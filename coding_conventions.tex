% vim: set noexpandtab:

\documentclass{scrartcl}

% Compilation instructions and required third party tools:
% - lualatex from texlive 2015 or newer
% - pygments from http://pygments.org (2.0.x or newer)
% - (already included) AnonymousPro font in OTF format from http://www.fontsquirrel.com/fonts/Anonymous-Pro

\usepackage{fontspec}
\setmonofont{AnonymousPro}

\usepackage[english]{babel}
\usepackage{minted}
\setminted{autogobble, tabsize=4, fontfamily=tt}
\setminted[igor]{style=igor}

\date{4.05.2020}
\newcommand{\version}{0.14}

\usepackage[%
pdfa=true,
pdfprintscaling=None,
plainpages=true,
pdfencoding=auto,
hidelinks,
pdfauthor={Thomas Braun, byte physics, thomas.braun@byte-physics.de}
]{hyperref}

\author{Thomas Braun <\url{thomas.braun@byte-physics.de}>}
\title{Coding Conventions for Igor Pro}
\publishers{Version: \version}
%
\begin{document}
%
\maketitle
%
\section{Procedures}
%
\begin{itemize}
	\item Allways put code into external procedure files stored directly on disk
%
	\item Filenames are built from the characters \mintinline{text}{[A-Za-z_-]} and end with \mintinline{text}{.ipf}
%
	\item The file encoding is OS-dependent but the used charset should always be restricted to ASCII characters.
		  Code parts exclusively used with Igor Pro 7 or higher should use UTF-8 as text encoding and specify \mintinline{igor}{#pragma TextEncoding = "UTF-8"}.
%
	\item The beginning of each procedure file has \mintinline{igor}{#pragma rtGlobals=3} with optional comment.
%
	\item Always use UNIX (LF) end-of-line style
\end{itemize}
%
\section{Whitespace and Comments}
%
\subsection*{Comments}
%
\begin{itemize}
	\item Use \texttt{doxygen} for documenting files, functions, macros and constants.\newline
	There is an AWK script available to use Igor Pro Files with Doxygen:\newline
	\url{https://github.com/byte-physics/doxygen-filter-ipf}
%
	\item Always add a space before a trailing comment as in
	\begin{minted}{igor}
		if(a < 0)
			b = 1
		else // positive numbers (including zero)
			b = 4711
		endif
	\end{minted}
%
	\item Prefer comments on separate lines instead of trailing comments
%
\end{itemize}
%
\subsection*{Doxygen}
\begin{itemize}
	\item Use \mintinline{igor}{///} to start a doxygen comment and \mintinline{igor}{///<} for documentation after the definition
%
	\item Align multiple \mintinline{igor}{@param} arguments and document them in the same order as in the function signature:
	\begin{minted}{igor}
		/// @param pressure     Pressure of the cell
		/// @param temperature  Outdoor temperature
		/// @param length       Length of a soccer field
		Function PerformCalculation(pressure, temperature, length)
			variable pressure, temperature, length

			// code
		End
	\end{minted}
%
	\item Use \mintinline{text}{in/out} specifiers for \mintinline{text}{@param} if the function uses call-by-value and call-by-reference parameters.
	\begin{minted}{igor}
		/// @param[in]  name    Name of the device
		/// @param[out] type    Device type
		/// @param[out] number  Device number
		Function ParseString(name, type, number)
			string name
			variable &type, &number

			// code
		End
	\end{minted}
%
	\item Optional parameters are documented as
	\begin{minted}{igor}
		/// @param verbose [optional, default = 0] Verbosely output
		///                the steps of the performed calculations
		Function DoCalculation([verbose])
			variable verbose

			// code
		End
	\end{minted}
\end{itemize}
%
\subsection*{Whitespace}
%
\begin{itemize}
	\item Every function should be separated by exactly one newline from other code
%
	\item Indentation is done with tabs, a tab consists of four spaces (in case you are coding not in Igor Pro).
%
	\item Comments on separate lines have the same indentation level as the surrounding code
%
	\item Separate function parameters from local variables and local variables from the rest of the function body by a newline
	\begin{minted}{igor}
		Function CalculatePressure(weight, size)
			variable weight, size

			variable i, numEntries

			// code
		End
	\end{minted}
%
  \item If you are targeting Igor Pro 7 or higher prefer inline parameter declarations as in
	\begin{minted}{igor}
		Function CalculatePressure(variable weight, variable size)
			variable i, numEntries

			// code
		End
	\end{minted}
  as that is easier to grasp for newcomers. And also works with multiple-return-value syntax.
%
	\item Add a space around mathematical/binary/comparison operators and assignments, and add a space after a comma or semicolon
	\begin{minted}{igor}
		a = b + c * (d + 1) / 5

		if(a < b)
			c = a^2 + b^2
		end

		Make/O/N={1, 2} data

		for(i = 0; i < numWaves; i += 1)
			a = i^2
		endfor

		if(myStatus && myClock)
			e = f
		endif
	\end{minted}
%
	\item Try to avoid trailing whitespace, here space is \FancyVerbSpace\normalfont\ and tab is \FancyVerbTab\par
	Good:\par
	\begin{minted}[showtabs,obeytabs,showspaces]{igor}
	Function DoStuff()
		print "Hi"

		if(a < b)
			c = a^2 + b^2
		end

		Make/O/N={1, 2} data
	End
	\end{minted}
	% Generated from
	%Function DoStuff()
	%print "Hi there"
	%
	%if(a < b)
	%c = a^2 + b^2
	%end
	%
	%Make/O/N={1, 2} data
	%End
	% with
	% pygmentize -l igor -f latex -F tokenmerge  -P style=igor -P commandprefix=PYGdefault -P stripnl=false -P "verboptions=,showtabs,obeytabs,showspaces" -o test.pyg "test.txt"
	% and then added \rule{0pt}{0pt} after every line with trailing whitespace.
	Bad:
	\begin{Verbatim*}[commandchars=\\\{\},,showtabs,obeytabs,showspaces,tabsize=4, gobble=1]
	\PYGdefault{n+nc}{Function} DoStuff() \rule{0pt}{0pt}
		\PYGdefault{n+nc}{print} \PYGdefault{k}{\PYGdefaultZdq{}Hi\PYGdefaultZdq{}} 	\rule{0pt}{0pt}
		\rule{0pt}{0pt}
		\PYGdefault{n+nc}{if}(a \PYGdefaultZlt{} b)    \rule{0pt}{0pt}
			c = a\PYGdefaultZca{}2 + b\PYGdefaultZca{}2	\rule{0pt}{0pt}
		\PYGdefault{n+nc}{end} \rule{0pt}{0pt}
		\rule{0pt}{0pt}
		\PYGdefault{n+nc}{Make}/O/N=\PYGdefaultZob{}1, 2\PYGdefaultZcb{} data \rule{0pt}{0pt}
	\PYGdefault{n+nc}{End}
	\end{Verbatim*}
%
	\item Surround blocks like \mintinline{igor}{if/endif, for/endfor, do/while, switch/endswitch,} \mintinline{igor}{strswitch/endswitch} with a newline if what they express is a logical group of its own
	\begin{minted}{igor}
	for(i = 0; i < numEntries; i += 1)
		// code
	endfor

	if(a > b)
		c = d
	elseif(a == b)
		c = e
	else
		c = 0
	endif

	switch(mode)
		case MODE1:
			a = "myString"
			break
		case MODE2:
			a = "someOtherString"
			break
		default:
			Abort "unknown mode"
			break
	endswitch
	\end{minted}
	According to that reasoning the following snippet has no newline before \mintinline{igor}{for} and \mintinline{igor}{if}
	\begin{minted}{igor}
	numEntries = ItemsInList(list)
	for(i = 0; i < numEntries; i += 1)
		// code
	endfor

	NVAR num = root:fancyNumber
	if(num < 5)
		// code
	endif
	\end{minted}
	When mutiple end statements match
	\begin{minted}{igor}
	for(i = 0; i < numEntries; i += 1)
		// code

		if(i < 5)
			// code
		endif
	endfor
	\end{minted}
	you should not add a trailing newline.
%
	\item There is no whitespace between different flags of an operation and no whitespace around \mintinline{igor}{=} if used in a flag assignment.\par
	Good:
	\begin{minted}{igor}
		Wave/Z/T/SDFR=dfr wv = myWave

		Function/S DoStuff()
			// code
		End
	\end{minted}
	Bad:
	\begin{minted}{igor}
		Wave /Z /T /SDFR = dfr wv = myWave
	\end{minted}
%
	\item The \mintinline{text}{&} in a call-by-reference parameter is attached to the name\par
	Good:
	\begin{minted}{igor}
	Function DoStuff(length, height, weight)
		variable &length, &height, &weight

		// code
	End
	\end{minted}
	Bad:
	\begin{minted}{igor}
		Function DoStuff(length, height, weight)
			variable& length, & height,& weight

			// code
		End
	\end{minted}
%
\end{itemize}
%
\section{Code}
%
\subsection{General}
%
\begin{itemize}
	\item Line length should not exceed 120 characters
%
	\item Use \mintinline{text}{camelCase} for variable/string/wave/dfref names and \mintinline{text}{CamelCase} for functions and structures
%
	\item Prefer structure-based GUI control procedures over old-style functions
%
	\item The variables \mintinline{text}{i, j, k, l} are reserved for loop counters, from outer to inner loops
%
	\item Use free waves for temporary waves
%
	\item Prefer generic builtin functions like \mintinline{igor}{IndexToScale, DimSize} over their 1D counterparts \mintinline{igor}{pnt2x, numpnts}.
%
	\item Write your code as much as possible without \mintinline{igor}{SetDataFolder}. Properly document if your function expects
	      a certain folder to be the current data folder at the time of the function call. Always restore the previously active current data folder
	      before returning from the function.
%
	\item Although Igor Pro code is case-insensitive use the offical upper/lower case as shown in the Igor Pro Help files for better readability
	\begin{minted}{igor}
		Make/N=(10) data
		AppendToGraph/W=$graph data
		WAVE/Z wv
		SVAR sv = abcd
		STRUCT Rectangular rect
		print ItemsInList(list)
	\end{minted}
	except for the following two cases:
	\begin{minted}{igor}
		variable storageCount
		string name
	\end{minted}
%
	\item Variable and function definitions and references to them must also never vary in case
%
	\item Don't use variables for storing the result which is then returned.\par
	Good:
	\begin{minted}{igor}
		if(someCondition)
			// code
			return 0
		else
			// code
			return 1
		endif
		// if it is important to know that the returned value
		// is a status, name the function something like GetStatusForFoo
		// and/or use the @return doxygen comment for explaining its meaning
	\end{minted}
	\begin{minipage}{\textwidth}
	Bad:
	\begin{minted}{igor}
		variable status

		// code

		if(someCondition)
			// code
			status = 0
		else
			// code
			status = 1
		endif

		return status
	\end{minted}
	\end{minipage}
%
	\item Avoid commented out code
%
	\item Don't initialize variables and strings if not required and always initialize variables in their own line.\par
	Good:
	\begin{minted}{igor}
		variable i = 1
		variable numEntries, maxLength
		string list
	\end{minted}
	Bad:
	\begin{minted}{igor}
		variable i = 0, numEntries = ItemsInList(list), maxLength
		string list = ""
	\end{minted}
%
	\item Don't use the default value for an optional argument\par
	Good:
	\begin{minted}{igor}
		StringFromList(0, list)
	\end{minted}
	Bad:
	\begin{minted}{igor}
		StringFromList(0, list, ";")
	\end{minted}
%
	\item Use parentheses sparingly\par
	Good:
	\begin{minted}{igor}
		variable a = b * (1 + 2)

		if(a < b || a < c)
			// code
		endif
	\end{minted}
	\begin{minipage}{\textwidth}
	Bad:
	\begin{minted}{igor}
		variable a = (b * (1 + 2))

		if((a < b) || (a < c))
			// code
		endif
	\end{minted}
	\end{minipage}
	\item Use parentheses when combining operators with the same precedence\par
	Good:
	\begin{minted}{igor}
		if((A || B) && C)
			// code
		endif

		if(A == (B >= C))
			// code
		endif
	\end{minted}
	Bad:
	\begin{minted}{igor}
		if(A || B && C) // same as above as these are left to right
			// code
		endif

		if(A == B >= C) // same as above as these are right to left
			// code
		endif
	\end{minted}
	The reason is that remembering the exact associativity is too error-prone. See also \mintinline{igor}{DisplayHelpTopic "Operators"}.
%
	\item Always add a space after \mintinline{igor}{;} when multiple statements
	are written in one line. But in general this should be avoided if possible.
%
	\item With try/catch always clear runtime errors twice
	\begin{minted}{igor}
		try
			err = getRTError(1)
			WAVE wv = I_DONT_EXIST; AbortOnRTE
		catch
			err = getRTError(1)
			// handle error
		endtry
	\end{minted}
	If you don't clear it after \mintinline{igor}{try} any still lingering
	runtime error will trigger an abort via \mintinline{igor}{AbortOnRTE} and that results in
	difficult to diagnose bugs.
%
	\item Don't mix \mintinline{igor}{$} with other expressions as it makes the code too hard to read\par
	Bad:\par
	\begin{minted}{igor}
		WAVE/Z wv = root:$(str + "_suffix")
	\end{minted}
	Good:\par
	\begin{minted}{igor}
		string folder = str + "_suffix"
		WAVE/Z wv = root:$folder
	\end{minted}
	The reason for this rule is that it makes the code easier to grasp, see\newline
	\mintinline{igor}{DisplayHelpTopic "$ Precedence Issues In Commands"} for the
	details how \mintinline{igor}{$} works in complex expressions.
%
	\item Always add \mintinline{igor}{break} statements in each branch of
	\mintinline{igor}{switch/strswitch} statements. If you intentionally
	fallthrough mark that by an explicit comment.
	\begin{minted}{igor}
		switch(state)
			case STATE_A:
				// do something
				break
			case STATE_B:
				// something else
				break
			caste STATE_C: // fallthrough-by-design
			caste STATE_D:
				// another thing
				break
			default:
				// do nothing
				break
		endswitch
	\end{minted}
\end{itemize}
	%
\subsection{Waves}
%
\begin{itemize}
	\item In multidimensional wave assignments always specify the exact dimension for each value:
	\begin{minted}{igor}
		Make/N=(1,1,2) data = NaN
		data[0][0][] = 0
	\end{minted}
	In this example data will be set to \mintinline{igor}{0} for both values.
	Each dimension is specified: p and q are fixed to \mintinline{igor}{0} and both values in dimension r are set to \mintinline{igor}{0}.
%
	\begin{minted}{igor}
		Make/N=(1,1,2) data = NaN
		data[0][0] = 0
	\end{minted}
	In this example the output will be \mintinline{igor}{0} and \mintinline{igor}{NaN} when using Igor Pro 7 (IP7).
	In Igor Pro 6 (IP6) the assignement will result in \mintinline{igor}{0} for both values. \newline
	The IP6 behaviour can be triggered in IP7 by setting an Igor Option:
	\begin{minted}{igor}
		SetIgorOption FuncOptimize, WaveEqn = 1
	\end{minted}
	To avoid confusing code always specify what value should go in which dimension (row, column, layer, chunk).
\end{itemize}
\subsection{Constants}
%
\begin{itemize}
	\item Static constants, which are required only in one file, should be defined at the top of the file
%
	\item Global constants are named with all caps and underlines and are collated in a single file
%
	\item Explain magic numbers in a comment
	\begin{minted}{igor}
		static Constant DEFAULT_WAVE_SIZE = 128 // equals 2^8 which is
		                                        // the width of the DAC signal
	\end{minted}
\end{itemize}
%
\subsection{Macros}
\begin{itemize}
	\item Use Macros only for window recreation macros
%
	\item Try to avoid changing window recreation macros by hand. Write instead a function to reset the panel to
	      the default state and let Igor Pro rewrite the macro by \mintinline{igor}{DoWindow/R}.
\end{itemize}
%
\subsection{Functions}
\begin{itemize}
	\item Try to keep their length below 50 lines (or half the screen height)
%
	\item Use \mintinline{text}{CamelCase} for function names (optionally prefixed by \mintinline{text}{SomeString_} denoting the filename)
%
	\item Make them \mintinline{igor}{static} if they are only required inside the same procedure file
%
	\item Define all variables at the top of the function body as in
	\begin{minted}{igor}
		Function CalculatePressure(weight, size)
			variable weight, size

			variable i, numEntries

			// code
		End
	\end{minted}
	The reason for this rule is that there is no block-scope in Igor Pro, i.\,e.
	\begin{minted}{igor}
	if(someCondition)
		variable a = 4711
	end

	print a
	\end{minted}
	is valid code. And that certainly will confuse people coming from C/C++.\\
%
	Please also note that in the example above a blank line separates function argument definitions from general variable definitions.
	This will improve readability.
%
	\item Optional arguments should have defined default values:
	\begin{minted}{igor}
		Function DoCalculation(input, [verbose])
			variable input, verbose

			if(ParamIsDefault(verbose))
				verbose = 0
			endif

			// code
		End
	\end{minted}
%
	\item Function Call with optional arguments:
	\begin{minted}[showspaces]{igor}
		DoCalculation(41, verbose = 1)
	\end{minted}
		When calling a function, each argument is separated by a comma followed by a whitespace.
		Optional arguments are set with surrounding white spaces before and after the equal sign.
%
	\item Boolean optional arguments should be forced to (0,1)
	\begin{minted}{igor}
		Function DoCalculation([overwrite])
			variable overwrite

			overwrite = ParamIsDefault(overwrite) ? 0 : !!overwrite

			if(overwrite)
				// Some Code
			endif

			if(!overwrite)
				// Negation will work as expected
			endif
		End
	\end{minted}
	The reason for this rule is that possibly unexpected behaviour should always be avoided.
	Without the double negation statement neither one of the above if statements would get triggered if \mintinline{igor}{overwrite=NaN}.\newline
	To make this clear look at the following example:
	The function will print 2 as NaN can not get evaluated.
	\begin{minted}{igor}
		Function NaNisNotBool()
			if(NaN)
				print 0
			elseif(!NaN)
				print 1
			else
				print 2
			endif
		End
	\end{minted}
%
	\item If you don't care about a function result, return \mintinline{igor}{NaN/""/$""}
	\begin{minted}{igor}
		Function Dostuff()

			if(!isSomethingToDo())
				return NaN
			endif

			// code
		End
	\end{minted}
	The reason for this rule is that it makes the code easier to understand as
	these are the default return values (without multiple-return-value syntax)
	used by Igor Pro.
%
	\item Set pass-by-reference parameters to a save default value immediately at
	the beginning of the function
	\begin{minted}{igor}
		Function Dostuff(param)
			variable &param

			param = NaN

			if(!isSomethingToDo())
				return NaN
			endif

			// code
		End
	\end{minted}
	The reason is that all function return paths should return well-defined
	values in the returned pass-by-reference parameters. If the passed parameter
	is a structure, write a structure initialization function to handle setting
	it to a safe default.
%
\item Be aware of the different initial values for return values when using multiple-return-value syntax.
	\begin{minted}{igor}
		Function [variable var] New()
			// code
		End

		Function Old()
			// code
		End
	\end{minted}
	The function \mintinline{igor}{New()} returns \mintinline{igor}{0.0} whereas
	\mintinline{igor}{Old()} returns \mintinline{igor}{NaN}.
\end{itemize}
%
\section{Avoid Memory Leaks}
%
\begin{itemize}
  \item When a functions that returns a free wave is called in an operation call or user-defined function call
directly as parameter the free wave is not freed and thus causes a memory leak.
To avoid these cases, free waves that are returned from functions must always assigned to a wave reference variable.
	\begin{minted}{igor}
		Function AvoidLeak()
			Duplicate ReturnsFreeWave(), targetWave // Causes leak, DO NOT USE
			max = WaveMax(ReturnsFreeWave()) // Causes leak, DO NOT USE

			WAVE w = ReturnsFreeWave() // good
			Duplicate w, targetWave

			WAVE w = ReturnsFreeWave() // good
			max = WaveMax(w)
		End
	\end{minted}
\end{itemize}

\section{Links and Literature}
%
\begin{itemize}
	\item ASCII: \url{https://en.wikipedia.org/wiki/ASCII}
%
	\item Doxygen: \url{http://www.stack.nl/~dimitri/doxygen/index.html}
%
	\item Git settings for Igor Pro code: \url{http://www.igorexchange.com/node/6013}
%
	\item Robert C. Martin, Clean Code: A Handbook of Agile Software Craftsmanship, Prentice Hall (2008)
%
	\item How to write good commit messages: \url{http://who-t.blogspot.de/2009/12/on-commit-messages.html}
\end{itemize}
%
\end{document}
